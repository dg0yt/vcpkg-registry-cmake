#
# Copyright 2021 Kai Pastor <dg0yt@darc.de>
#
# License: MIT

if(NOT DEFINED PORT)
    message(FATAL_ERROR "Need a PORT variable")
endif()

set(BINARY_DIR        "@CMAKE_CURRENT_BINARY_DIR@")
set(REGISTRY_CHECKOUT "@REGISTRY_CHECKOUT@")
set(VCPKG_ROOT        "@VCPKG_ROOT@")
set(vcpkg             [[@vcpkg@]])

include("${BINARY_DIR}/update-configuration.cmake")

message(STATUS "Running vcpkg x-add-version ${PORT}")
foreach(overwrite_option IN ITEMS "" "--overwrite-version")
    execute_process(
      COMMAND ${vcpkg} x-add-version "${PORT}"
        --skip-formatting-check
        ${overwrite_option}
        "--x-builtin-ports-root=${REGISTRY_CHECKOUT}/ports"
        "--x-builtin-registry-versions-dir=${REGISTRY_CHECKOUT}/versions"
      WORKING_DIRECTORY "${VCPKG_ROOT}"
      OUTPUT_VARIABLE add_version_output
      RESULT_VARIABLE result
    )
    if(add_version_output MATCHES "No local Git SHA was found")
        set(result 0)
        break()
    elseif(NOT add_version_output MATCHES "--overwrite-version")
        break()
    endif()
endforeach()
if(NOT result EQUAL 0)
    message(FATAL_ERROR ${add_version_output})
endif()
